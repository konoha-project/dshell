import env TEST_DIR
import command echo

let LOG = "./test-result.log"

function test(test_dir : String, test_file : String, option : String) {
	import command java

	var mode = "[s]"
	if(option == "-i") {
		mode = "[i]"
	}
	echo "====== Test: ${mode} ${test_file} ======" &>> $LOG
	let result : boolean = java -jar -ea test-dshell.jar $option $test_dir/$test_file &>> $LOG
	var prefix = "[fail]:"
	if(result) {
		prefix = "[pass]:"
	}
	var msg = "${prefix} ${mode} ${test_dir}/${test_file}"
	echo $msg
	echo $msg &>> $LOG

	return result
}

function test_all(dir : String, option : int) {
	import command ls
	var count = 0
	var pass_count = 0
	for(test_file in $(ls $dir)) {
		if(option == 0) {
			count++
			if(test(dir, test_file, "")) {
				pass_count++
			}
			count++
			if(test(dir, test_file, "-i")) {
				pass_count++
			}
		} else if(option == 1) {
			count++
			if(test(dir, test_file, "")) {
				pass_count++
			}
		} else {
			count++
			if(test(dir, test_file, "-i")) {
				pass_count++
			}
		}
	}
	log "test:${dir} ${pass_count}/${count}"
}

echo > $LOG
test_all("${TEST_DIR}/common", 0)
test_all("${TEST_DIR}/scripting", 1)

